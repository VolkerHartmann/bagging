plugins {
  id 'java'
  
  //these have to be in the main project for now see - https://discuss.gradle.org/t/how-do-i-include-buildscript-block-from-external-gradle-script/7016/2
  id "com.github.kt3k.coveralls" version "2.8.4" 
  id "de.aaschmid.cpd" version "3.1"
  id "org.owasp.dependencycheck" version "5.2.4"
//  id "com.dorongold.task-tree" version "1.4"
  id "org.ajoberstar.grgit" version "4.0.0"
  id "com.github.spotbugs" version "2.0.1"
  id "com.jfrog.bintray" version "1.8.4"
}
apply from: 'eclipse.gradle'
apply from: 'bintray.gradle'
//apply from: 'maven-central.gradle'
apply from: 'code-quality.gradle'
apply from: 'message-bundle.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.version == "unspecified"){
  String now = new Date().format( 'MMM-dd-yyyy_HH-mm-ss' )
  project.version = "1.0.0-${now}-SNAPSHOT"
}

repositories {
  jcenter()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:1.7.28', 
  'com.fasterxml.jackson.core:jackson-core:2.10.0', 
  'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2', 
    'org.springframework.boot:spring-boot-starter-logging:2.1.9.RELEASE',
    'org.bouncycastle:bcprov-jdk15on:1.63',
    'org.kamranzafar:jtar:2.3'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test { 
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
  reports {
    html.enabled = true
  }
  //testLogging.showStandardStreams = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
      xml.enabled = false
      html.enabled = true
  }
}

import org.ajoberstar.grgit.*
task cloneConformanceSuite(){
  group "Verification"
  description "Download the bagit-conformance-suite if it doesn't exist."
  File location = file("${project.projectDir}/bagit-conformance-suite")
  outputs.dir(location)
  
  onlyIf { !location.exists() }
  doLast{
      Grgit.clone(dir: location, 
                  uri: 'https://github.com/libraryofcongress/bagit-conformance-suite.git')
    }
}